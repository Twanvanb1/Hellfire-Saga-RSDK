// ----------------------------------
// RSDK Project:  Hellfire Saga
// Script Description: Tite Setup Object 
// Script Author: Twanvanb1
// ----------------------------------

// ========================
// Aliases
// ========================


private alias object.value0 : object.timer
private alias object.value1 : object.deformTimer
private alias object.value2 : object.fadeTimer
private alias object.value3 : object.fadeInTimer
private alias object.value4 : object.fadeOutTimer
private alias object.value5 : object.drawPos.y
private alias object.value6 : object.drawStart.y
private alias object.value7 : object.moveDelay
private alias object.value8 : object.delayTimer
private alias object.value9 : object.deformAmount

private alias 0	:	Title_State_Init
private alias 1	:	Title_State_FadeIn
private alias 2	:	Title_State_ShowLogo
private alias 3	:	Title_State_FadeInFlames
private alias 4	:	Title_State_PressStart
private alias 5	:	Title_State_FlashStart
private alias 6	:	Title_State_MoveLogo
private alias 7	:	Title_State_ToLevel

// Deformation Layer
private alias 2	:	DEFORM_BG

// Ink Effect Aliases
private alias 0 : INK_NONE   // Disables transparency
private alias 1 : INK_BLEND  // Uses half transparency
private alias 2 : INK_ALPHA  // Uses transparency value of Object.Alpha
private alias 3 : INK_ADD    // Uses transparency value of Object.Alpha, and a bright tint
private alias 4 : INK_SUB    // Uses transparency value of Object.Alpha, and a dark tint


// ========================
// Function Declarations
// ========================


// ========================
// Static Values
// ========================

// ========================
// Tables
// ========================

// ========================
// Function Definitions
// ========================

// ========================
// Events
// ========================

event ObjectUpdate
    object.deformTimer++
	if object.deformTimer > 1
		tileLayer[0].deformationOffset += 4
        if object.state > Title_State_FadeInFlames
		    tileLayer[2].deformationOffset++
        end if
		object.deformTimer = 0
	end if
    
    temp0 = screen.xcenter
    FlipSign(temp0)
    temp0 += 288
    screen.xoffset = temp0
    screen.yoffset = -8

    

    switch object.state
    case Title_State_Init

        object.drawPos.y = screen.ycenter
        object.drawPos.y -= 24

        object.drawStart.y = screen.ycenter
        object.drawStart.y += 72
        object.alpha = 0
        object.scale = 0
        object.fadeInTimer = 512
        object.deformAmount = 1
        object.state++
        break
    case Title_State_FadeIn
        if object.fadeInTimer > 0
            object.fadeInTimer -= 8
        else
            object.fadeInTimer = 0
        end if

        if object.alpha > 0
            object.alpha--
        end if

        if object.fadeInTimer == 0
            object.state++
        end if
        break

    case Title_State_ShowLogo
        if object.alpha <= 0x100
            object.alpha += 6
        end if

        if object.alpha >= 0x100
            object.state++
        end if
        break

    case Title_State_FadeInFlames
        if object.fadeTimer < 256
            object.fadeTimer += 8
        else
            object.timer = 0
            object.state++
        end if
        SetPaletteFade(0, 2, 1, object.fadeTimer, 176, 191)
        break

    case Title_State_PressStart
        object.timer++
        if object.timer == 32
            object.timer = 0
            object.visible ^= true
        end if

#platform: MOBILE
        CheckTouchRect(0, 0, screen.xsize, screen.ysize)
        if checkResult > -1
            object.timer = 0
            object.state = Title_State_FlashStart
            object.moveDelay = 48
        end if
#endplatform

        if keypress[0].start == true
            object.timer = 0
            object.state = Title_State_FlashStart
            object.moveDelay = 48
        end if
        break

    case Title_State_FlashStart
        object.timer++
        if object.timer == 2
            object.timer = 0
            object.visible ^= true
        end if

        object.moveDelay--
        if object.moveDelay <= 0
            object.visible = true
            object.drawStart.y += 4
        end if

        temp0 = screen.ysize
        temp0 += 16
        if object.drawStart.y > temp0
            object.state = Title_State_MoveLogo
        end if
        break

    case Title_State_MoveLogo
        object.drawPos.y -= 4
        if object.drawPos.y < -72
            object.state = Title_State_ToLevel
        end if
        break

    case Title_State_ToLevel
        object.fadeOutTimer += 8
        if object.fadeOutTimer == 320
            options.attractMode 	= false
			starPostID 				= 0
			specialStage.listPos 	= 0
			specialStage.emeralds 	= 0
			specialStage.nextZone 	= 0

			// New Game - to FDZ1 you go!
			stage.activeList 		= REGULAR_STAGE
			stage.listPos 			= 0
			LoadStage()
		end if
        SetScreenFade(0, 0, 0, object.fadeOutTimer)
        break

    end switch

    SetLayerDeformation(0, 128, object.fadeInTimer, 0, 0, 0)

    if object.state > Title_State_FadeInFlames
	    SetLayerDeformation(2, 32, 8, 0, 0, 0)
        if object.state < 6
            object.angle++
            Sin(temp1, object.angle)
            temp1 >>= 6
            object.drawPos.y = screen.ycenter
            object.drawPos.y -= 24
            object.drawPos.y += temp1
        end if
    end if
end event

event ObjectDraw
    object.inkeffect = INK_ALPHA
    DrawSpriteScreenFX(0, FX_INK, screen.xcenter, object.drawPos.y)

    

    if object.state > Title_State_FadeInFlames
        object.inkeffect = INK_BLEND
        if object.visible == true
#platform: STANDARD
            DrawSpriteScreenFX(1, FX_INK, screen.xcenter, object.drawStart.y)
#endplatform
#platform: MOBILE
            DrawSpriteScreenFX(2, FX_INK, screen.xcenter, object.drawStart.y)
#endplatform
        end if
    end if
end event

event ObjectStartup
    LoadSpriteSheet("Title/Title.gif")
    SpriteFrame(-145, -71, 290, 143, 1, 1)
    SpriteFrame(-37, -9, 75, 18, 1, 145)
    SpriteFrame(-46, -9, 93, 18, 1, 164) 

    temp0 = 0
    while temp0 <= 255
        SetPaletteEntry(2, temp0, 0x000000)
        
        temp0++
    loop

    CopyPalette(0, 176, 1, 176, 16)
    CopyPalette(2, 176, 0, 176, 16)

    tileLayer[2].scrollPos 		= -0x8 0000
    
end event

// ========================
// Editor Events
// ========================

event RSDKDraw
    DrawSprite(0)
end event

event RSDKLoad
    LoadSpriteSheet("Title/Title.gif")
    SpriteFrame(-145, -71, 290, 143, 1, 1)

  SetVariableAlias(ALIAS_VAR_PROPVAL, "Does this work?")
end event
