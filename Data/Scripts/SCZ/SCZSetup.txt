// ----------------------------------
// RSDK Project: Hellfire Saga RSDKv4
// Script Description:    SCZ Setup Object 
// Script Author: Twanvanb1
// ----------------------------------

// ========================
// Aliases
// ========================


private alias object.value0 : object.paletteTimer
private alias object.value1 : object.deformTimer

// Tracks
private alias 0 : TRACK_STAGE
private alias 1 : TRACK_ACTFINISH
private alias 2 : TRACK_INVINCIBLE
private alias 3 : TRACK_CONTINUE
private alias 4 : TRACK_BOSS
private alias 5 : TRACK_GAMEOVER
private alias 6 : TRACK_DROWNING
private alias 7 : TRACK_SUPER

// Reserved Object Slots Aliases
private alias 10 : SLOT_ZONESETUP
private alias 25 : SLOT_MUSICEVENT_CHANGE
private alias 26 : SLOT_MUSICEVENT_BOSS

// Music Events
private alias 0 : MUSICEVENT_FADETOBOSS
private alias 1 : MUSICEVENT_FADETOSTAGE
private alias 2 : MUSICEVENT_TRANSITION

private alias 0 : MUSICEVENT_FLAG_NOCHANGE
private alias 1 : MUSICEVENT_FLAG_SPEEDUP
private alias 2 : MUSICEVENT_FLAG_SLOWDOWN

// Music Loops

private alias 152750 : MUSIC_LOOP_SCZ
private alias 122240 : MUSIC_LOOP_SCZ_F

private alias 38679  : MUSIC_LOOP_INV
private alias 30897  : MUSIC_LOOP_INV_F

// Deformation Layer
private alias 2	:	DEFORM_BG

// ========================
// Function Declarations
// ========================

reserve function SCZSetup_SpeedUpMusic
reserve function SCZSetup_SlowDownMusic

// ========================
// Static Values
// ========================

// ========================
// Tables
// ========================

private table CSZSetup_deformationTable
	0, 1, 1, 0, 0, 1, 1, 0, 1, 0, 1, 1, 0, 1, 1, 0 // 0x10
	1, 0, 0, 1, 1, 0, 0, 1, 0, 1, 0, 0, 1, 1, 0, 0 // 0x20
	1, 1, 0, 1, 1, 0, 1, 0, 0, 1, 1, 0, 0, 1, 1, 0 // 0x30
	0, 1, 0, 1, 1, 0, 1, 1, 0, 0, 1, 1, 0, 1, 1, 0 // 0x40
	1, 0, 0, 1, 0, 0, 1, 0, 1, 1, 0, 1, 0, 0, 1, 1 // 0x50
	0, 1, 0, 1, 1, 0, 1, 0, 1, 0, 0, 1, 1, 0, 1, 1 // 0x60
	0, 1, 1, 0, 1, 0, 1, 0, 1, 1, 0, 1, 1, 0, 1, 1 // 0x70
	0, 0, 1, 0, 0, 1, 1, 0, 1, 0, 1, 1, 0, 1, 1, 0 // 0x80
	1, 0, 1, 1, 0, 1, 1, 0, 1, 1, 0, 0, 1, 0, 0, 1 // 0x90
	1, 0, 0, 1, 1, 0, 0, 1, 1, 0, 1, 1, 0, 0, 1, 0 // 0xA0
end table

// ========================
// Function Definitions
// ========================

private function SCZSetup_SpeedUpMusic
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].type, TypeName[Music Event])
	temp0 = checkResult
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].propertyValue, MUSICEVENT_TRANSITION)
	temp0 &= checkResult
	CheckEqual(stage.musicFlag, MUSICEVENT_FLAG_NOCHANGE)
	temp0 &= checkResult
	if temp0 == 0
		switch music.currentTrack
		case TRACK_STAGE
			SetMusicTrack("Invincibility_F.ogg", TRACK_INVINCIBLE, MUSIC_LOOP_INV_F)
			SwapMusicTrack("EmeraldHill_F.ogg", TRACK_STAGE, MUSIC_LOOP_SCZ_F, 8000)
			break

		case TRACK_INVINCIBLE
			SetMusicTrack("EmeraldHill_F.ogg", TRACK_STAGE, MUSIC_LOOP_SCZ_F)
			SwapMusicTrack("Invincibility_F.ogg", TRACK_INVINCIBLE, MUSIC_LOOP_INV_F, 8000)
			break

		case TRACK_BOSS
		case TRACK_DROWNING
		case TRACK_SUPER
			SetMusicTrack("EmeraldHill_F.ogg", TRACK_STAGE, MUSIC_LOOP_SCZ_F)
			SetMusicTrack("Invincibility_F.ogg", TRACK_INVINCIBLE, MUSIC_LOOP_INV_F)
			break

		end switch
	else
		stage.musicFlag = MUSICEVENT_FLAG_SPEEDUP
	end if
end function

private function SCZSetup_SlowDownMusic
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].type, TypeName[Music Event])
	temp0 = checkResult
	CheckEqual(object[SLOT_MUSICEVENT_CHANGE].propertyValue, MUSICEVENT_TRANSITION)
	temp0 &= checkResult
	CheckEqual(stage.musicFlag, MUSICEVENT_FLAG_NOCHANGE)
	temp0 &= checkResult
	if temp0 == 0
		switch music.currentTrack
		case TRACK_STAGE
			SetMusicTrack("Invincibility.ogg", TRACK_INVINCIBLE, MUSIC_LOOP_INV)
			SwapMusicTrack("EmeraldHill.ogg", TRACK_STAGE, MUSIC_LOOP_SCZ, 12500)
			break

		case TRACK_INVINCIBLE
			SetMusicTrack("EmeraldHill.ogg", TRACK_STAGE, MUSIC_LOOP_SCZ)
			SwapMusicTrack("Invincibility.ogg", TRACK_INVINCIBLE, MUSIC_LOOP_INV, 12500)
			break

		case TRACK_BOSS
		case TRACK_DROWNING
		case TRACK_SUPER
			SetMusicTrack("EmeraldHill.ogg", TRACK_STAGE, MUSIC_LOOP_SCZ)
			SetMusicTrack("Invincibility.ogg", TRACK_INVINCIBLE, MUSIC_LOOP_INV)
			break

		end switch
	else
		stage.musicFlag = MUSICEVENT_FLAG_SLOWDOWN
	end if
end function

// ========================
// Events
// ========================

event ObjectUpdate
	//object.paletteTimer++
	//if object.paletteTimer > 8
	//	object.paletteTimer = 0
	//	RotatePalette(0, 128, 175, true)
	//end if

	object.deformTimer++
	if object.deformTimer > 3
		tileLayer[1].deformationOffset++
		object.deformTimer = 0
	end if

	if object.animationTimer == 0
		object.animationTimer = 8
		object.frame++
		object.frame %= 4
		temp0 = 368
		temp0 += object.frame
		Copy16x16Tile(16, temp0)
		temp0 += 4
		Copy16x16Tile(17, temp0)
		temp0 += 4
		Copy16x16Tile(18, temp0)
		temp0 += 4
		Copy16x16Tile(19, temp0)
		temp0 += 4
		Copy16x16Tile(20, temp0)
		temp0 += 4
		Copy16x16Tile(21, temp0)
	else
		object.animationTimer--
	end if
end event

event ObjectDraw
end event

event ObjectStartup
	SetMusicTrack("EmeraldHill.ogg", TRACK_STAGE, MUSIC_LOOP_SCZ)

	SpeedUpMusic = SCZSetup_SpeedUpMusic
	SlowDownMusic = SCZSetup_SlowDownMusic

	animalType1 = TypeName[Flicky]
	animalType2 = TypeName[Ricky]

	object[SLOT_ZONESETUP].type = TypeName[SCZ Setup]
	object[SLOT_ZONESETUP].priority = PRIORITY_ACTIVE

	temp0 = screen.xcenter
	FlipSign(temp0)
	temp0 += 160
	temp0 <<= 16
	hParallax[0].scrollPos = temp0

	arrayPos0 = 0
	while arrayPos0 < 576
		temp0 = arrayPos0
		temp0 %= 0xA0
		GetTableValue(stage.deformationData2[arrayPos0], temp0, CSZSetup_deformationTable)
		arrayPos0++
	loop

	FlipSign(hParallax[2].scrollSpeed)

end event

// ========================
// Editor Events
// ========================

event RSDKDraw
  DrawSprite(0)
end event

event RSDKLoad
  LoadSpriteSheet("Global/Display.gif")
  SpriteFrame(-16, -16, 32, 32, 1, 143)

  SetVariableAlias(ALIAS_VAR_PROPVAL, "Does this work?")
end event
